# This file is part of CynuxSecurity Linux ( https://www.cynuxsecurity.org/ ).
# See COPYING for license details.

pkgname=angrop
pkgver=183.b9f33d6
pkgrel=2
pkgdesc='A rop gadget finder and chain builder.'
groups=('cynuxsecurity' 'cynuxsecurity-exploitation')
arch=('any')
url='https://github.com/salls/angrop'
license=('BSD')
depends=('python' 'python-progressbar' 'python-pyvex' 'python-claripy'
         'python-simuvex' 'angr')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/salls/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/tests" tests/*
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

