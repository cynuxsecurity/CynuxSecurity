# Maintainer: CynuxSecurity <team@cynuxsecurity.org>

buildarch=1

pkgname=responder
pkgver=2.3.4.0
pkgrel=1
groups=('cynuxsecurity' 'cynuxsecurity-networking')
pkgdesc="A LLMNR and NBT-NS poisoner, with built-in HTTP/SMB/MSSQL/FTP/LDAP rogue authentication server supporting NTLMv1/NTLMv2/LMv2, Extended Security NTLMSSP and Basic HTTP authentication."
url="https://github.com/lgandx/Responder"
license=('custom')
arch=('any')
depends=('python2')
makedepends=('unzip')
source=("${url}/archive/v$pkgver.zip" 'LICENSE')
sha512sums=('587f92af79b0a3a3e3d7b2838dd6131227419f4c18d1e02c0d8ebe2e2fc099d74e0a95976f7b62c1f7c6b83a8e6c3215385dc67853f2b69a32e45817e3730ae0'
            '49254b769f161d19ade5858a4eb4eba7ce791051cb04bdabba4184ee8409e7bb83e3ec0941dfe3fc71d09fe5abff6d6d8b22b4ec8ffaa82f63a3dd91cc979551')

prepare() {
  grep -iRl 'python' "$srcdir" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir"/Responder-"$pkgver"
  install -dm755 "$pkgdir/usr/share/responder"
  install -dm755 "$pkgdir/usr/bin"
  cp -r poisoners tools servers certs files "$pkgdir/usr/share/responder"
  install -m755 odict.py "$pkgdir"/usr/share/responder/odict.py
  install -m755 fingerprint.py "$pkgdir"/usr/share/responder/fingerprint.py
  install -m755 packets.py "$pkgdir"/usr/share/responder/packets.py
  install -m755 settings.py "$pkgdir"/usr/share/responder/settings.py
  install -m755 utils.py "$pkgdir"/usr/share/responder/utils.py
  install -m644 README.md "$pkgdir"/usr/share/responder/README.md
  install -m644 Responder.conf "$pkgdir"/usr/share/responder/Responder.conf
  install -m755 Responder.py "$pkgdir"/usr/share/responder/Responder.py
  install -Dm644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/responder/LICENSE
  sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python|#!/usr/bin/env python2|' $pkgdir/usr/share/responder/servers/HTTP.py
  cat > "$pkgdir/usr/bin/responder" <<EOF
#!/usr/bin/env bash
cd /usr/share/responder
python2 Responder.py "\$@"
EOF
  chmod 755 "$pkgdir/usr/bin/responder"
}

