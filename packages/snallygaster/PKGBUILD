# This file is part of CynuxSecurity Linux ( https://www.cynuxsecurity.org/ ).
# See COPYING for license details.

pkgname=snallygaster
pkgver=164.bfe8914
pkgrel=2
pkgdesc='Tool to scan for secret files on HTTP servers.'
groups=('cynuxsecurity' 'cynuxsecurity-webapp' 'cynuxsecurity-scanner')
arch=('any')
url='https://github.com/hannob/snallygaster'
license=('custom:unknown')
depends=('python' 'python-beautifulsoup4' 'python-argparse' 'python-urllib3'
         'python-dnspython')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/hannob/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr --skip-build

  install -Dm 644 -t "$pkgdir/usr/share/$pkgname/" Dockerfile
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md TESTS.md \
    CONTRIBUTIONS.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

